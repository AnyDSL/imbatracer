
struct BVHNode
{
    bbox : BBox,
    sndChildFirstPrim : uint, // for an inner node, the second child. for a leaf, the first primitive.
    nPrim : u16, // number of primitives in child. 0 for inner node.
    axis: u16,
}

struct BVHTraversal
{
    todo : [uint*16], // sync this with BVH construction in C++!
}
fn emptyBVHTraversal() -> BVHTraversal
{
    @BVHTraversal { todo:[0u,0u,0u,0u, 0u,0u,0u,0u, 0u,0u,0u,0u, 0u,0u,0u,0u] }
}

fn bvhBBoxComp(bounds : BBox, comp : bool) -> Point
{
    if comp { bounds.cmax } else { bounds.cmin }
}

fn bvhDirIsNeg(neg : uint, axis : uint) -> bool
{
    (neg & (1u << axis)) != 0u
}

fn bvhMakeNeg(invDir : Vec) -> uint
{
    (bool2uint(invDir.x < 0.0f)      ) |
    (bool2uint(invDir.y < 0.0f) << 1u) |
    (bool2uint(invDir.z < 0.0f) << 2u)
}

fn bvhIntersectNode(bounds : BBox, o : Point, maxDist : float, invDir : Vec, neg: uint) -> bool
{
    // Check for ray intersection against x and y slabs
    let txmin = (@bvhBBoxComp(bounds,  @bvhDirIsNeg(neg, 0u)).x - o.x) * invDir.x;
    let txmax = (@bvhBBoxComp(bounds, !@bvhDirIsNeg(neg, 0u)).x - o.x) * invDir.x;
    let tymin = (@bvhBBoxComp(bounds,  @bvhDirIsNeg(neg, 1u)).y - o.y) * invDir.y;
    let tymax = (@bvhBBoxComp(bounds, !@bvhDirIsNeg(neg, 1u)).y - o.y) * invDir.y;
    if (txmin > tymax) || (tymin > txmax)
    { false }
    else {
        let tmin = max(txmin, tymin);
        let tmax = min(txmax, tymax);
        // Check for ray intersection against z slab
        let tzmin = (@bvhBBoxComp(bounds,  @bvhDirIsNeg(neg, 2u)).z - o.z) * invDir.z;
        let tzmax = (@bvhBBoxComp(bounds, !@bvhDirIsNeg(neg, 2u)).z - o.z) * invDir.z;
        if  (tmin > tzmax) || (tzmin > tmax)
        { false }
        else
        {
            max(tmin, tzmin) < maxDist && min(tmax, tzmax) >= 0.0f
        }
    }
}

fn bvhIntersectFirst(root: uint, scene : &Scene, mut scratch : &RTScratch) -> ()
{
    let isec = &(*scratch).isec;
    let invDir = @vapply1((*isec).ray.d, |f|1.0f/f);
    let neg = @bvhMakeNeg(invDir);

    // we roll our own stack
    let done = return;
    let mut todo = &(*scratch).bvh.todo;
    let mut todoIdx = 0; // the first *free* index

    // CPS fun while passing the current node around as argument :-)
    for curNode in ever1(root) { // we shouldn't unroll this, but adding a $ here makes stuff go slower
        // stack helper functions
        fn pushNode(node : uint) -> ()
        {
            (*todo)(todoIdx) = node; // do the left child later
            todoIdx = todoIdx+1;
        }
        fn popNode()
        {
            if todoIdx == 0 {
                done() // DONE!
            }
            else {
                // continue with stack element
                todoIdx = todoIdx-1;
                continue((*todo)(todoIdx))
            }
        }

        if @bvhIntersectNode((*scene).bvhNodes(curNode).bbox, (*isec).ray.o, (*isec).dist, invDir, neg)
        {
            // inner loop: we assume the current node does not intersect
            let inner = continue;

            let nPrim = (*scene).bvhNodes(curNode).nPrim;
            let sndChildFirstPrim = (*scene).bvhNodes(curNode).sndChildFirstPrim;
            if (nPrim > 0u16) {
                // a leaf
                for tri in urange(sndChildFirstPrim, sndChildFirstPrim+(nPrim as uint)) {
                    triIntersectFirst(tri, scene, isec);
                }
                // done with this node, pop one off the stack
                @popNode()
            }
            else {
                fn descend(fstNode : uint, sndNode : uint) {
                    @pushNode(sndNode);
                    continue(fstNode)
                }
                // an inner node, which child do we get to first?
                if @bvhDirIsNeg(neg, (*scene).bvhNodes(curNode).axis as uint) {
                    @descend(sndChildFirstPrim, curNode+1u) // do the left child later
                }
                else {
                    @descend(curNode+1u, sndChildFirstPrim) // do the right child later
                }
            }
        }
        else
        {
            // we missed this node, pop one off the stack
            @popNode()
        }
    };
}
